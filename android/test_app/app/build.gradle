apply plugin: 'com.android.application'

repositories {
    jcenter()
}

def props = new Properties()
file("../gradle.properties").withInputStream { props.load(it) }

def buildConfigProps = { k -> "\"${props.get(k)}\"" }

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    defaultConfig {
        applicationId "org.pytorch.testapp"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 1
        versionName "1.0"
        ndk {
            abiFilters ABI_FILTERS.split(",")
        }
        buildConfigField ("String", "MODULE_ASSET_NAME", buildConfigProps('MODULE_ASSET_NAME'))
        buildConfigField ("String", "LOGCAT_TAG", "@string/app_name")
        buildConfigField ("int", "TU_TYPE", "0")
        buildConfigField ("int", "FORWARD_COUNT", "50")
        addManifestPlaceholders([APP_NAME: "@string/app_name", MAIN_ACTIVITY: "org.pytorch.testapp.MainActivity"])
    }
    buildTypes {
        debug {
            minifyEnabled false
        }
    }
    flavorDimensions "model"
    productFlavors {
        ttt {
            dimension "model"
            applicationIdSuffix ".ttt"
            buildConfigField ("String", "MODULE_ASSET_NAME", "\"print.pt\"")
            addManifestPlaceholders([APP_NAME: "TEST"])
            buildConfigField ("String", "LOGCAT_TAG", "\"pytorch-test\"")
        }
        mbq {
            dimension "model"
            applicationIdSuffix ".mbq"
            buildConfigField ("String", "MODULE_ASSET_NAME", buildConfigProps('MODULE_ASSET_NAME_MOBNET2_QUANT'))
            addManifestPlaceholders([APP_NAME: "MBQ"])
            buildConfigField ("String", "LOGCAT_TAG", "\"pytorch-mbq\"")
        }
        mbqCamera {
            dimension "model"
            applicationIdSuffix ".mbqCamera"
            buildConfigField ("String", "MODULE_ASSET_NAME", buildConfigProps('MODULE_ASSET_NAME_MOBNET2_QUANT'))
            addManifestPlaceholders([APP_NAME: "MBQ_C", MAIN_ACTIVITY: "org.pytorch.testapp.CameraActivity"])
            buildConfigField ("String", "LOGCAT_TAG", "\"pytorch-mbqCamera\"")
            buildConfigField ("int", "TU_TYPE", "0")
        }
        mbqCameraC1 {
            dimension "model"
            applicationIdSuffix ".mbqCameraC1"
            buildConfigField ("String", "MODULE_ASSET_NAME", buildConfigProps('MODULE_ASSET_NAME_MOBNET2_QUANT'))
            addManifestPlaceholders([APP_NAME: "MBQ_C1", MAIN_ACTIVITY: "org.pytorch.testapp.CameraActivity"])
            buildConfigField ("String", "LOGCAT_TAG", "\"pytorch-mbqCamera-C1\"")
            buildConfigField ("int", "TU_TYPE", "1")
        }
        mbqCameraC2 {
            dimension "model"
            applicationIdSuffix ".mbqCameraC2"
            buildConfigField ("String", "MODULE_ASSET_NAME", buildConfigProps('MODULE_ASSET_NAME_MOBNET2_QUANT'))
            addManifestPlaceholders([APP_NAME: "MBQ_C2", MAIN_ACTIVITY: "org.pytorch.testapp.CameraActivity"])
            buildConfigField ("String", "LOGCAT_TAG", "\"pytorch-mbqCamera-C2\"")
            buildConfigField ("int", "TU_TYPE", "2")
        }
        mbqCameraL {
            dimension "model"
            applicationIdSuffix ".mbqCameraL"
            buildConfigField ("String", "MODULE_ASSET_NAME", buildConfigProps('MODULE_ASSET_NAME_MOBNET2_QUANT'))
            addManifestPlaceholders([APP_NAME: "MBQ_CAMERA_L", MAIN_ACTIVITY: "org.pytorch.testapp.CameraActivity"])
            buildConfigField ("String", "LOGCAT_TAG", "\"pytorch-mbqCamera-L\"")
            buildConfigField ("int", "TU_TYPE", "3")
        }
        resnet18 {
            dimension "model"
            applicationIdSuffix ".resneti18"
            buildConfigField ("String", "MODULE_ASSET_NAME", buildConfigProps('MODULE_ASSET_NAME_RESNET18'))
            addManifestPlaceholders([APP_NAME: "PyResNet18"])
            buildConfigField ("String", "LOGCAT_TAG", "\"pytorch-resnet18\"")
        }
    }
    packagingOptions {
        doNotStrip '**.so'
    }
}

dependencies {
    implementation 'com.android.support:appcompat-v7:28.0.0'

    implementation project(':pytorch_android')
    implementation project(':pytorch_android_torchvision')

    def camerax_version = "1.0.0-alpha05"
    implementation "androidx.camera:camera-core:$camerax_version"
    implementation "androidx.camera:camera-camera2:$camerax_version"
    implementation 'com.google.android.material:material:1.0.0-beta01'

}
